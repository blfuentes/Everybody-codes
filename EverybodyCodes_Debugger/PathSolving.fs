module PathSolving

open System.Collections.Generic

type Kind = Free | Wall | Start | Goal
type Coord = int * int
type Cell = { Pos: Coord; Kind: Kind }


let maze15x13 : int[,] = array2D [
    [1;1;1;1;1;1;1;1;1;1;1;1;1]
    [1;2;0;0;1;0;0;0;1;0;0;3;1]
    [1;0;1;0;1;0;1;0;1;0;1;1;1]
    [1;0;1;0;0;0;1;0;0;0;0;0;1]
    [1;0;1;1;1;0;1;1;1;1;1;0;1]
    [1;0;0;0;1;0;0;0;0;0;1;0;1]
    [1;1;1;0;1;1;1;1;1;0;1;0;1]
    [1;0;0;0;0;0;0;0;1;0;1;0;1]
    [1;0;1;1;1;1;1;0;1;0;1;0;1]
    [1;0;1;0;0;0;1;0;1;0;1;0;1]
    [1;0;1;0;1;0;1;0;1;0;1;0;1]
    [1;0;1;0;1;0;1;0;1;0;1;0;1]
    [1;0;0;0;1;0;0;0;1;0;0;0;1]
    [1;1;1;0;1;1;1;1;1;1;1;1;1]
    [1;1;1;1;1;1;1;1;1;1;1;1;1]
]

let buildMaze (data: int[,]) =
    let maxRows = data.GetLength(0)
    let maxCols = data.GetLength(1)
    let maze = Array2D.zeroCreate<Cell> maxRows maxCols 
    for rIdx in 0..maxRows-1 do
        for cIdx in 0..maxCols-1 do
            let t' = 
                match data[rIdx, cIdx] with
                | 0 -> Free
                | 1 -> Wall
                | 2 -> Start
                | 3 -> Goal
                | _ -> failwith "error"
            maze[rIdx, cIdx] <- { Pos = (rIdx, cIdx); Kind = t' }
    maze

///////////////
let themaze = buildMaze maze15x13
///////////////

let getNeighbors (maze: Cell[,]) (x, y) =
    let rows = maze.GetLength(0)
    let cols = maze.GetLength(1)
    [ (x-1, y); (x+1, y); (x, y-1); (x, y+1) ]
    |> List.filter (fun (r, c) ->
        r >= 0 && r < rows && c >= 0 && c < cols &&
        match maze[r, c].Kind with Wall -> false | _ -> true
    )

let findKind (maze: Cell[,]) kind =
    maze
    |> Seq.cast<Cell>
    |> Seq.find (fun cell -> cell.Kind = kind)
    |> fun cell -> cell.Pos


// Flood fill
let floodFill (maze: Cell[,]) : Set<Coord> =
    let start = findKind maze Start
    let visited = HashSet<Coord>()
    let queue = Queue<Coord>()
    queue.Enqueue(start)
    visited.Add(start) |> ignore

    while queue.Count > 0 do
        let current = queue.Dequeue()
        for n in getNeighbors maze current do
            if not (visited.Contains(n)) then
                visited.Add(n) |> ignore
                queue.Enqueue(n)

    visited |> Set.ofSeq

let resultFloodFill = floodFill themaze
printfn "Floodfill: %A" resultFloodFill

// Flood fill all paths
let floodFillAllPaths (maze: Cell[,]) : Coord list list =
    let start = findKind maze Start
    let goal = findKind maze Goal

    let results = ResizeArray<Coord list>()

    let rec flood visited path current =
        if current = goal then
            results.Add(List.rev (current :: path))
        else
            for neighbor in getNeighbors maze current do
                if not (Set.contains neighbor visited) then
                    flood (Set.add neighbor visited) (current :: path) neighbor

    flood (Set.singleton start) [] start
    results |> Seq.toList

let allPathsFloodFill = floodFillAllPaths themaze
let shortestFloodFill = allPathsFloodFill |> List.minBy List.length
printfn "Flood fill all paths"
allPathsFloodFill |> List.iteri(fun i p -> 
        printfn "Flood fill path %A. Length: %A :: %A" (i+1) p.Length p
)

// Floodfill steps
let floodFillSteps (maze: Cell[,]) : Dictionary<Coord, int> =
    let start = findKind maze Start

    let rows = maze.GetLength(0)
    let cols = maze.GetLength(1)

    let steps = Dictionary<Coord, int>()
    let queue = Queue<Coord>()
    queue.Enqueue(start)
    steps[start] <- 0

    while queue.Count > 0 do
        let current = queue.Dequeue()
        let currentSteps = steps.[current]
        for neighbor in getNeighbors maze current do
            if not (steps.ContainsKey(neighbor)) then
                steps[neighbor] <- currentSteps + 1
                queue.Enqueue(neighbor)

    steps

// Floodfill steps with parents
let floodFillStepsWithParents (maze: Cell[,]) : Dictionary<Coord, int> * Dictionary<Coord, Coord> =
    let start = findKind maze Start

    let rows = maze.GetLength(0)
    let cols = maze.GetLength(1)

    let steps = Dictionary<Coord, int>()
    let parent = Dictionary<Coord, Coord>()
    let queue = Queue<Coord>()
    queue.Enqueue(start)
    steps.[start] <- 0

    while queue.Count > 0 do
        let current = queue.Dequeue()
        let currentSteps = steps.[current]
        for neighbor in getNeighbors maze current do
            if not (steps.ContainsKey(neighbor)) then
                steps[neighbor] <- currentSteps + 1
                parent[neighbor] <- current
                queue.Enqueue(neighbor)

    steps, parent

let steps, parent = floodFillStepsWithParents themaze
let goal = themaze |> Seq.cast<Cell> |> Seq.find (fun c -> c.Kind = Goal) |> fun c -> c.Pos

let rec buildPath acc pos =
    if parent.ContainsKey(pos) then buildPath (pos :: acc) parent.[pos]
    else pos :: acc

let pathToGoal = buildPath [] goal
printfn "Steps to goal: %d" steps.[goal]
printfn "Path: %A" pathToGoal

// DFS
let dfs (maze: Cell[,]) =
    let start = findKind maze Start
    let goal = findKind maze Goal
    let visited = HashSet<Coord>()
    let parent = Dictionary<Coord, Coord>()

    let rec search pos =
        if pos = goal then true
        else
            visited.Add(pos) |> ignore
            getNeighbors maze pos
            |> List.exists (fun n ->
                if not (visited.Contains(n)) then
                    parent[n] <- pos
                    search n
                else false
            )

    let found = search start
    let rec buildPath acc pos =
        if pos = start then start :: acc
        elif parent.ContainsKey(pos) then buildPath (pos :: acc) parent[pos]
        else []

    let path = if found then buildPath [] goal else []
    path.Length, path

let resultDFS = dfs themaze
printfn "DFS: %A" resultDFS

// DFS All paths
let dfsAllPaths (maze: Cell[,]) : Coord list list =
    let start = findKind maze Start
    let goal = findKind maze Goal

    let rows = maze.GetLength(0)
    let cols = maze.GetLength(1)

    let isValid (x, y) =
        x >= 0 && x < rows && y >= 0 && y < cols &&
        match maze[x, y].Kind with Wall -> false | _ -> true

    let getNeighbors (x, y) =
        [ (x-1, y); (x+1, y); (x, y-1); (x, y+1) ]
        |> List.filter isValid

    let results = ResizeArray<Coord list>()

    let rec dfs visited path current =
        if current = goal then
            results.Add(List.rev (current :: path))
        else
            for neighbor in getNeighbors current do
                if not (Set.contains neighbor visited) then
                    dfs (Set.add neighbor visited) (current :: path) neighbor

    dfs (Set.singleton start) [] start
    results |> Seq.toList

let allPathdsDfs = dfsAllPaths themaze
let shortestDfs = allPathdsDfs |> List.minBy List.length
printfn "DFS all paths"
allPathdsDfs |> List.iteri(fun i p -> 
        printfn "DFS path %A. Length: %A :: %A" (i+1) p.Length p
)

// BFS
let bfs (maze: Cell[,]) =
    let start = findKind maze Start
    let goal = findKind maze Goal
    let visited = HashSet<Coord>()
    let parent = Dictionary<Coord, Coord>()
    let queue = Queue<Coord>()
    queue.Enqueue(start)
    visited.Add(start) |> ignore

    while queue.Count > 0 do
        let current = queue.Dequeue()
        if current = goal then queue.Clear()
        for n in getNeighbors maze current do
            if not (visited.Contains(n)) then
                visited.Add(n) |> ignore
                parent[n] <- current
                queue.Enqueue(n)

    let rec buildPath acc pos =
        if pos = start then start :: acc
        elif parent.ContainsKey(pos) then buildPath (pos :: acc) parent[pos]
        else []

    let path = buildPath [] goal
    path.Length, path

let resultBFS = bfs themaze
printfn "BFS: %A" resultBFS

// DIJKSTRA
let dijkstra (maze: Cell[,]) =
    let start = findKind maze Start
    let goal = findKind maze Goal

    let dist = Dictionary<Coord, int>()
    let parent = Dictionary<Coord, Coord>()
    let visited = HashSet<Coord>()

    // Priority queue: (distance, position)
    let pq = SortedSet<(int * Coord)>(Comparer<(int * Coord)>.Create(fun (d1, c1) (d2, c2) ->
        if d1 <> d2 then compare d1 d2 else compare c1 c2
    ))

    dist[start] <- 0
    pq.Add((0, start)) |> ignore

    while pq.Count > 0 do
        let (d, current) = pq.Min
        pq.Remove((d, current)) |> ignore

        if visited.Contains(current) then
            () // already processed
        else
            visited.Add(current) |> ignore

            for neighbor in getNeighbors maze current do
                let alt = d + 1 // uniform cost
                if not (dist.ContainsKey(neighbor)) || alt < dist[neighbor] then
                    dist[neighbor] <- alt
                    parent[neighbor] <- current
                    pq.Add((alt, neighbor)) |> ignore

    // Path reconstruction
    let rec buildPath acc pos =
        if pos = start then start :: acc
        elif parent.ContainsKey(pos) then buildPath (pos :: acc) parent[pos]
        else []

    let path = buildPath [] goal
    path.Length, path

let resultDijkstra = dijkstra themaze
printfn "Dijkstra: %A" resultDijkstra

// A*
let manhattan (x1, y1) (x2, y2) = abs(x1 - x2) + abs(y1 - y2)

let aStar (maze: Cell[,]) =
    let start = findKind maze Start
    let goal = findKind maze Goal
    let gScore = Dictionary<Coord, int>()
    let fScore = Dictionary<Coord, int>()
    let parent = Dictionary<Coord, Coord>()
    let openSet = SortedSet<(int * Coord)>(Comparer<(int * Coord)>.Create(fun (f1, c1) (f2, c2) ->
        if f1 <> f2 then compare f1 f2 else compare c1 c2
    ))

    gScore[start] <- 0
    fScore[start] <- manhattan start goal
    openSet.Add((fScore[start], start)) |> ignore

    while openSet.Count > 0 do
        let (_, current) = openSet.Min
        openSet.Remove((fScore[current], current)) |> ignore
        if current = goal then openSet.Clear()
        for n in getNeighbors maze current do
            let tentativeG = gScore[current] + 1
            if not (gScore.ContainsKey(n)) || tentativeG < gScore[n] then
                parent[n] <- current
                gScore[n] <- tentativeG
                fScore[n] <- tentativeG + manhattan n goal
                openSet.Add((fScore[n], n)) |> ignore

    let rec buildPath acc pos =
        if pos = start then start :: acc
        elif parent.ContainsKey(pos) then buildPath (pos :: acc) parent[pos]
        else []

    let path = buildPath [] goal
    path.Length, path

let resultAStar = aStar themaze
printfn "A Star: %A" resultAStar